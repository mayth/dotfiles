# load
autoload -Uz add-zsh-hook
autoload -Uz is-at-least
autoload -Uz colors

### load z - jump around
if [ -f ~/.zsh/z/z.sh ]; then
  . ~/.zsh/z/z.sh
fi

### key binding
bindkey -e

### misc
setopt no_beep
setopt hist_expand        # auto expand history in completion
setopt magic_equal_subst  # filename expand
REPORTTIME=30             # report the used time when the process used this time.

### directory moving
setopt auto_cd
setopt auto_pushd

### completion
autoload -U compinit && compinit
fpath=(/usr/local/share/zsh-completions $fpath)

zstyle ':completion:*' format '%B%d%b'
zstyle ':completion:*' group-name ''
zstyle ':completion:*:descriptions' format '%U%B%d%b%u'
zstyle ':completion:*:warnings' format '%BSorry, no matches for: %d%b'
zstyle ':completion:*:default' menu select=2
zstyle ':completion:*:default' list-colors ""
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Z} r:|[._-]=*'
zstyle ':completion:*' use-cache yes
zstyle ':completion:*' verbose yes
zstyle ':completion:sudo:*' environ PATH="$SUDO_PATH:$PATH"
zstyle ':completion:*' ignore-parents parent pwd ..
zstyle ':completion:*:*files' ignored-patterns '*?.o' '*?~' '*\#'

### history
# set history size
export HISTSIZE=2000
export HISTFILE="$HOME/.history"
export SAVEHIST=$HISTSIZE

# ignore duplicate commands and begin with space
setopt hist_ignorealldups
setopt hist_ignorespace

### enable extended-glob
setopt extendedglob

### path
# add sbin to path when completion with sudo
[ -z "$sudo_path" ] && typeset -xT SUDO_PATH sudo_path
typeset -U sudo_path
sudo_path=({,/usr,/usr/local}/sbin(N-/))

### grep
export GREP_OPTIONS
# GREP_OPTIONS="--binary-files=without-match --directories=recurse --exclude=\*.tmp --exclude-dir=.svn --exclude-dir=.git --exclude-dir=.deps --exclude-dir=.libs --color=auto"
GREP_OPTIONS="--binary-files=without-match --exclude=\*.tmp --exclude-dir=.svn --exclude-dir=.git --exclude-dir=.deps --exclude-dir=.libs --color=auto"

### Prompt setting
setopt prompt_subst
setopt transient_rprompt

default_rprompt="%F{green}[%d]%f"
case ${UID} in
# for root user
0)
  PROMPT="%B%F{yellow}%n@%m%k %B%F{blue}%c %# %b%f%k"
  PROMPT2="%B%F{yellow}%n@%m%k %B%F{blue}%c %_> %b%f%k"
  SPROMPT="zsh: correct '%R' to '%r' [No, Yes, Abort, Edit]?"
  [ -n "${REMOTEHOST}${SSH_CONNECTION}" ] &&
    PROMPT="%F{magenta}${HOST%%.*}$f ${PROMPT}"
  ;;
# for normal user
*)
  local kakashi="ただのカカシですな"$'\n'
  local empty=""
  PROMPT="%(?.${empty}.${kakashi})%B%F{green}%n@%m%k %B%F{blue}%c %# %b%f%k"
  PROMPT2="%B%F{green}%n@%m%k %B%F{blue}%c %_> %b%f%k"
  SPROMPT="zsh: correct %B'%R'%b to %B'%r'%b [No, Yes, Abort, Edit]? "
  [ -n "${REMOTEHOST}${SSH_CONNECTION}" ] &&
    PROMPT="%F{magenta}${HOST%%.*}%f ${PROMPT}"
  ;;
esac

### vcs_info
autoload -Uz vcs_info
if [ -f ~/.zsh/vcs_info.zsh ]; then
  source ~/.zsh/vcs_info.zsh
fi

### incremental search
if [ -f ~/.zsh/auto-fu.zsh/auto-fu.zsh ]; then
  source ~/.zsh/auto-fu.zsh/auto-fu.zsh
  function zle-line-init () {
    auto-fu-init
  }
  zle -N zle-line-init
  zstyle ':auto-fu:var' postdisplay $''
  zle -N zle-keymap-select auto-fu-zle-keymap-select
fi
zstyle ':completion:*' completer _list _complete _expand _approximate _match _prefix

### pager
if [ -n "${PAGER#lv}" ]; then
  export LV="-c -l" # -c to enable ASCII escape sequence coloring, -l to no line break in copying
fi


### user definition functions
# create new window on connecting with tmux&mosh
if [ $TERM = screen ]; then
  function mosh_tmux() {
    eval server=\${$#}
    tmux new-window -n $@ "exec ssh $@"
  }
  alias mosh=mosh_tmux
fi

# completion for mosh
function _mosh_hosts {
  local -a config_hosts
  local config
  integer ind

  # If users-hosts matches, we shouldn't complete anything else.
  if [[ "$IPREFIX" == *@ ]]; then
    _combination -s '[:@]' my-accounts users-hosts "users=${IPREFIX/@}" hosts "$@" && return
  else
    _combination -s '[:@]' my-accounts users-hosts \
      ${opt_args[-l]:+"users=${opt_args[-l]:q}"} hosts "$@" && return
  fi
  if (( ind = ${words[(I)-F]} )); then
    config=${~words[ind+1]}
  else
    config="$HOME/.ssh/config"
  fi
  if [[ -r $config ]]; then
    local IFS=$'\t ' key hosts host
    while read key hosts; do
      if [[ "$key" == (#i)host ]]; then
   for host in ${(z)hosts}; do
      case $host in
      (*[*?]*) ;;
      (*) config_hosts+=("$host") ;;
      esac
   done
      fi
    done < "$config"
    if (( ${#config_hosts} )); then
      _wanted hosts expl 'remote host name' \
  compadd -M 'm:{a-zA-Z}={A-Za-z} r:|.=* r:|=*' "$@" $config_hosts
    fi
  fi
}

compdef _mosh_hosts mosh

### Editor
if builtin command -v vim > /dev/null; then
  export EDITOR='vim'
elif builtin command -v vi > /dev/null; then
  export EDITOR='vi'
elif builtin command -v nano > /dev/null; then
  export EDITOR='nano'
fi

# Auto exec tmux
#if ( ! test $TMUX ) && ( ! expr $TERM : "^screen" > /dev/null ) && which tmux > /dev/null; then
#    if ( tmux has-session ); then
#        session=`tmux list-sessions | grep -e '^[0-9].*]$' | head -n 1 | sed -e 's/^\([0-9]\+\).*$/\1/'`
#            if [ -n "$session" ]; then
#            echo "Attache tmux session $session."
#                tmux attach-session -t $session
#        else
#            echo "Session has been already attached."
#            tmux list-sessions
#            fi
#    else
#        echo "Create new tmux session."
#        tmux
#    fi
#fi

### direnv
if which direnv > /dev/null; then
  eval "$(direnv hook zsh)"
fi

### Rake completion
if [ -f ~/.zsh/rake.zsh ]; then
  source ~/.zsh/rake.zsh
fi

### Added by the Heroku Toolbelt
if which heroku > /dev/null; then
  export PATH="/usr/local/heroku/bin:$PATH"
fi

### pyenv
if [ -x "$HOME/.pyenv/bin/pyenv" ]; then
  export PATH="$HOME/.pyenv/bin:$PATH"
  eval "$(pyenv init -)"
  eval "$(pyenv virtualenv-init -)"
fi

### Make alias 'pbcopy/pbpaste' if they does not exist
if ! which pbcopy > /dev/null; then
  if which xsel > /dev/null; then
    alias pbcopy='xsel --clipboard --input'
    alias pbpaste='xsel --clipboard --output'
  elif which xclip > /dev/null; then
    alias pbcopy='xclip -selection clipboard'
    alias pbpaste='xclip -selection clipboard -o'
  else
    echo "no clipboard interface found. install 'xsel' or 'xclip'."
  fi
fi

if [ -f ~/.zshrc.local ]; then
  source ~/.zshrc.local
fi

